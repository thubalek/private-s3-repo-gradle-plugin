plugins {
    id 'java-gradle-plugin'
    alias(libs.plugins.gradle.plugin.publish)
    alias(libs.plugins.kotlin.jvm)
    id 'groovy'
}

def getVersionAccordingTheTag() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--abbrev=0', '--tags'
        standardOutput = stdout
    }
    String verByGit = stdout.toString().trim()
    return verByGit
}

group = 'net.hubalek.gradle.s3privaterepo'
version = getVersionAccordingTheTag()

gradlePlugin {
    website = 'https://github.com/thubalek/private-s3-repo-gradle-plugin'
    vcsUrl = 'https://github.com/thubalek/private-s3-repo-gradle-plugin.git'

    plugins {
        placeholdersPlugin {
            id = 'net.hubalek.gradle.s3privaterepo'
            displayName = "S3 Private Repo Gradle Plugin"
            description = "Allows to add Amazon S3 private repo just according system properties or environment variables."
            implementationClass = 'net.hubalek.gradle.s3privaterepo.S3PrivateRepo'
            tags = ["s3", "aws", "credentials"]
        }
    }
}

repositories {
    mavenCentral()
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(libs.versions.java.get() as Integer)
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = libs.versions.java.get()
    }
}